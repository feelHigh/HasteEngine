#ifndef _FUNC
#define _FUNC

#include "Global.hlsli"
#include "Struct.hlsli"

int IsBinding(Texture2D _tex)
{
    int width = 0, height = 0;
    _tex.GetDimensions(width, height);
    
    if (0 == width || height == 0)
        return false;
    
    return true;
}

void CalculateLight2D(int _LightIdx, float3 _WorldPos, inout tLight _Light)
{
    tLightInfo Info = g_Light2D[_LightIdx];
    
    // DirectionalLight 인 경우
    if (0 == Info.Type)
    {
        //_Light.Color.rgb += Info.light.Color.rgb;
        _Light.Ambient.rgb += Info.LightEssence.Ambient.rgb;
    }
    
    // PointLight 인 경우
    else if (1 == Info.Type)
    {
         // 점광원과 픽셀까지의 거리
        /*
        float fDist = distance(Info.WorldPosition.xy, _WorldPosition.xy);
        
        // 거리값을 각도로 치환해서 거리에 따른 빛의 세기를 코사인 그래프 형태로 사용한다.
        float fPow = saturate(cos((fDist / Info.Radius) * (PI / 2.f)));
        
        // 광원으로부터 떨어진 거리에 따른 빛의 세기
        //float fPow2 = saturate(1.f - fDist / Info.Radius);
        
        // 최종 색상 계산 = 빛의 색 * 거리에따른 세기
        _Light.Color.rgb += Info.LightInfo.Color.rgb * fPow;
        _Light.Ambient.rgb += Info.LightInfo.Ambient.rgb;
        */
        float fAttenu = 1.f;
        
        float fDist = distance(Info.WorldPos.xy, _WorldPos.xy);
        if (fDist < Info.Radius)
        {
            if (g_int_0)
            {
                float fTheta = (fDist / Info.Radius) * (PI / 2.f);
                fAttenu = saturate(cos(fTheta));
            }
            else
            {
                fAttenu = saturate(1.f - fDist / g_Light2D[0].Radius);
            }
            
            _Light.Color += Info.LightEssence.Color * fAttenu;
        }
    }
    
    // SpotLight 인 경우
    else
    {
        float fAttenu = 1.f;

        float fDist = distance(Info.WorldPos.xy, _WorldPos.xy);
        float2 toPoint = normalize(Info.WorldPos.xy - _WorldPos.xy); // 현재위치-> 광원위치로 향하는 방향벡터
        float cosTheta = dot(normalize(Info.WorldDir.xy), toPoint); // 빛이 향하는 방향과 빛을 받는 지점까지 벡터간의 각도
        
        if (fDist < Info.Radius)
        {
            // cosTheta > info.fAngle / 2.f : 현재 픽셀이 발산하는 빛의 각도 범위 내에 있음
            if (cosTheta > cos(radians(Info.Angle) / 2.f))
            {
                float fTheta = (fDist / Info.Radius) * (PI / 2.f);
                fAttenu = saturate(cos(fTheta));
            
                _Light.Color += Info.LightEssence.Color * fAttenu;
            }
        }
    }
}


void CalculateLight3D(int _LightIdx, float3 _ViewNormal, float3 _ViewPos, inout tLight _Light)
{
    tLightInfo LightInfo = g_Light3D[_LightIdx];
    
    float LightPow = 0.f;
    float SpecularPow = 0.f;
    float Ratio = 1.f;
    float SpecRatio = 1.f;
    
    // Directional Light
    if (0 == LightInfo.Type)
    {
        // VS 에서 받은 노말값으로, 빛의 세기를 PS 에서 직접 구한다음 빛의 세기를 적용
        float3 vLightDir = normalize(mul(float4(LightInfo.WorldDir, 0.f), matView).xyz);
        LightPow = saturate(dot(-vLightDir, _ViewNormal));
            
        // 반사광 계산
        // vR = vL + 2 * dot(-vL, vN) * vN;
        float3 vReflect = vLightDir + 2 * dot(-vLightDir, _ViewNormal) * _ViewNormal;
        vReflect = normalize(vReflect);
            
        // 카메라에서 물체를 향하는 vEye 를 구한다. 카메라는 원점에 있다.
        // 픽셀의 뷰스페이스 위치가 곧 카메라에서 물체를 향하는 Eye 방향이다.
        float3 vEye = normalize(_ViewPos);
    
        // 반사 방향과 시선 벡터를 내적해서 둘 사이의 벌어진 각도에 대한 cos 값을 반사광의 세기로 사용한다.
        SpecularPow = saturate(dot(vReflect, -vEye));
        SpecularPow = pow(SpecularPow, 15);
    }
    
    // Point Light
    else if (1 == LightInfo.Type)
    {
        // 표면 위치에서 광원의 위치를 뺀다. 광원에서 표면을 향하는 방향벡터를 구할 수 있다.
        float3 vLightViewPos = mul(float4(LightInfo.WorldPos, 1.f), matView).xyz;
        float3 vLightDir = normalize(_ViewPos - vLightViewPos);
        LightPow = saturate(dot(-vLightDir, _ViewNormal));
            
        // 반사광 계산
        // vR = vL + 2 * dot(-vL, vN) * vN;
        float3 vReflect = vLightDir + 2 * dot(-vLightDir, _ViewNormal) * _ViewNormal;
        vReflect = normalize(vReflect);
            
        // 카메라에서 물체를 향하는 vEye 를 구한다. 카메라는 원점에 있다.
        // 픽셀의 뷰스페이스 위치가 곧 카메라에서 물체를 향하는 Eye 방향이다.
        float3 vEye = normalize(_ViewPos);
    
        // 반사 방향과 시선 벡터를 내적해서 둘 사이의 벌어진 각도에 대한 cos 값을 반사광의 세기로 사용한다.
        SpecularPow = saturate(dot(vReflect, -vEye));
        SpecularPow = pow(SpecularPow, 15);
             
        // 거리에 따른 빛의 세기 감소량을 계산한다.
        float Distance = length(vLightViewPos - _ViewPos);
        float CamDistance = length(_ViewPos);
        //Ratio = saturate(1.f - (Distance / LightInfo.Radius));
        Ratio = saturate(cos((PI / 2.f) * saturate(Distance / LightInfo.Radius)));
        SpecRatio = saturate(cos((PI / 2.f) * saturate(CamDistance / LightInfo.Radius)));
    }
    
    // Spot Light
    else if (2 == LightInfo.Type)
    {
        
    }
      
    // 표면이 받는 빛 = 광원의 빛 * 표면이 받는 빛의 세기 * 거리에 따른 빛의 감소량
    _Light.Color += LightInfo.LightEssence.Color * LightPow * Ratio;
    _Light.Ambient += LightInfo.LightEssence.Ambient * Ratio;
    _Light.SpecCoef += LightInfo.LightEssence.SpecCoef * SpecularPow * SpecRatio;
}

float3 GetRandom(in Texture2D _NoiseTexture, uint _ID, uint _maxId)
{
    float2 vUV = (float2) 0.f;
    vUV.x = ((float) _ID / (float) (_maxId - 1)) + g_EngineTime * 0.5f;
    vUV.y = sin(vUV.x * 20 * PI) * 0.5f + g_EngineTime * 0.1f;
    float3 vRandom = _NoiseTexture.SampleLevel(g_sam_1, vUV, 0).xyz;
    
    return vRandom;
}

#endif